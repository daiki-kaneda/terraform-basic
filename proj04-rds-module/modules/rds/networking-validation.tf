##################################
# サブネットのバリデーション
##################################
data "aws_vpc" "default" {
  default = true
}
data "aws_subnet" "input" {
  for_each = toset(var.subnet_ids)
  id       = each.value

  lifecycle {
    postcondition {
      condition     = self.vpc_id != data.aws_vpc.default.id
      error_message = <<-EOT
      以下のサブネットはデフォルトVPCに含まれています。

      ${can(self.tags.Name) ? "Name = ${self.tags.Name}" : ""}
      ID   = ${self.id}

      RDSインスタンスをデフォルトVPCにデプロイしないでください。
      EOT
    }

    postcondition {
      condition     = can(lower(self.tags.Access) == "private")
      error_message = <<-EOT
      以下のサブネットはプライベートとマークされていません。

      ${can(self.tags.Name) ? "Name = ${self.tags.Name}" : ""}
      ID   = ${self.id}

      サブネットに以下のタグがついていることを確認してください。
      1. Access = "private"
      EOT
    }
  }
}
##################################
# セキュリティグループのバリデーション
##################################

data "aws_vpc_security_group_rules" "input" {
  filter {
    name   = "group-id"
    values = var.security_group_ids
  }
}

data "aws_vpc_security_group_rule" "input" {
  for_each               = toset(data.aws_vpc_security_group_rules.input.ids)
  security_group_rule_id = each.value

  lifecycle {
    postcondition {
      condition = (
        self.is_egress ? true :
        self.cidr_ipv4 == null &&
        self.cidr_ipv6 == null &&
        self.referenced_security_group_id != null
      )
      error_message = <<-EOT
      以下のセキュリティグループは不適切なインバウンドルールが含まれています。

      ID = ${self.security_group_id}

      以下の条件が満たされていることを確認してください。
      1. ルールはIP CIDRブロックからのトラフィックを許可しないでください。セキュリティグループからのインバウンドトラフィックのみを許可してください。
      EOT
    }
  }
}